PROCESS LookupManager  "Manage the lookup dialogues in the survey"
USES tester
     Skólar 'SKOLAR\SKOLAR'
     Nám    'Namsleidir\Namsleidir'

INPUTFILE Skólalisti: Skólar('SKOLAR\SKOLAR',blaise)
SETTINGS
 LINKFIELDS
     SkoliID
INPUTFILE Námsgreinar: Nám('Namsleidir\Namsleidir',blaise)
LINKFIELDS
     fastanumer
TEMPORARYFILE survey : tester
SETTINGS
    INTERCHANGE = TRANSIT

AUXFIELDS
  dialogResult : (Fannst, EkkertFannst,HættaVið)
  searchtext   : STRING

DIALOGBOX SkolarLookup
  ESCAPE=NO
  LOOKUP Skólalisti
    SEARCH = YES
    SEARCHTEXT = '^searchtext'
    FIELDS Skolaheiti
  BUTTON dialogResult
    CAPTION = 'Velja'
    STORE = YES
    VALUE = Fannst
  BUTTON dialogResult
    CAPTION = 'Ekkert Fannst'
    STORE = YES
    VALUE = EkkertFannst
  BUTTON dialogResult
    STORE = YES
    VALUE = HættaVið
    CAPTION = 'Hætta við'

DIALOGBOX NamLookup
  ESCAPE = NO
  LOOKUP Námsgreinar
    SEARCH = YES
    SEARCHTEXT = '^searchtext'
    BESTFIT = YES
    FIELDS StigNáms
           SviðNáms
           Námsgrein
           
  BUTTON dialogResult
    CAPTION = 'Velja'
    STORE = YES
    VALUE = Fannst
  BUTTON dialogResult
    CAPTION = 'Ekkert Fannst'
    STORE = YES
    VALUE = EkkertFannst
  BUTTON dialogResult
    STORE = YES
    VALUE = HættaVið
    CAPTION = 'Hætta við'

DIALOGBOX MenntunLookup
  ESCAPE = NO
  LOOKUP Námsgreinar
    SEARCH = YES
    SEARCHTEXT = '^searchtext'
    BESTFIT = YES
    FIELDS StigNáms {todo: hafa tvö mismunandi stig fyrir nám og menntun}
           SviðNáms
           Námsgrein
           
  BUTTON dialogResult
    CAPTION = 'Velja'
    STORE = YES
    VALUE = Fannst
  BUTTON dialogResult
    CAPTION = 'Ekkert Fannst'
    STORE = YES
    VALUE = EkkertFannst
  BUTTON dialogResult
    STORE = YES
    VALUE = HættaVið
    CAPTION = 'Hætta við'

PROCEDURE prc_skoliLookup
    IF ROUTERSTATUS = BLRSEDIT THEN
        searchtext := survey.GETVALUE('O.OSkHvaða')
        SkolarLookup
        IF(dialogResult<>HættaVið) THEN
           IF((Skólalisti.Skolaheiti=EMPTY) OR (dialogResult=EkkertFannst)) THEN
               Skólalisti.GET(9999)
               survey.putvalue('O.Skólastig',EMPTY)
               survey.putvalue('O.OSkstig',EMPTY)
           ELSE
               survey.putvalue('O.Skólastig',STR(ORD(Skólalisti.stig)))
           ENDIF
           survey.PUTVALUE(ACTIVEFIELD,Skólalisti.Skolaheiti)
           survey.putvalue('O.OSklID',STR(skólalisti.skoliID))
        ENDIF
        SETALIENROUTERACTION(BLRADEFAULT)
    ENDIF
ENDPROCEDURE

PROCEDURE prc_namLookup
    IF ROUTERSTATUS = BLRSEDIT THEN
        searchtext := survey.GETVALUE('O.ONamHvað')
        NamLookup
        IF(dialogResult<>HættaVið) THEN
          IF dialogResult = EkkertFannst  THEN
            Námsgreinar.GET(9999)
          ENDIF
            survey.putvalue(ACTIVEFIELD,Námsgreinar.Námsgrein)
            survey.putvalue('O.skóti',Námsgreinar.StigNáms)
            survey.putvalue('O.OSklKóti',STR(Námsgreinar.fastanumer))          
        ENDIF
        SETALIENROUTERACTION(BLRADEFAULT)
    ENDIF
ENDPROCEDURE

PROCEDURE prc_menntunLookup
    IF ROUTERSTATUS = BLRSEDIT THEN
        searchtext := survey.GETVALUE('P.PHvað')
        MenntunLookup
        IF(dialogResult<>HættaVið) THEN
          IF dialogResult = EkkertFannst  THEN
            Námsgreinar.GET(9999)
          ENDIF
            survey.putvalue(ACTIVEFIELD,Námsgreinar.Námsgrein)
            survey.putvalue('P.mKóti',Námsgreinar.StigNáms)
            survey.putvalue('P.PMenntNr',STR(Námsgreinar.fastanumer))          
        ENDIF
        SETALIENROUTERACTION(BLRADEFAULT)
    ENDIF
ENDPROCEDURE


DIALOGBOX Test2
    SIZE = (360,200)
    CONTROL Skólalisti.Skolaheiti
        POSITION = (120,25)
    CONTROL Skólalisti.Stig
        POSITION = (120,50)
    BUTTON
        CAPTION = 'OK'
        SIZE = (75,25)

MANIPULATE
  searchtext := parameter(1)
  NamLookup
  Test2

