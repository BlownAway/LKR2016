PROCESS LookupManager  "Manage the lookup dialogues in the survey"
USES tester
     Skólar 'SKOLAR\SKOLAR'
     Nám    'Namsleidir\Namsleidir'

INPUTFILE Skólalisti: Skólar('SKOLAR\SKOLAR',blaise)
SETTINGS
 LINKFIELDS
     SkoliID
INPUTFILE Námsgreinar: Nám('Namsleidir\Namsleidir',blaise)
LINKFIELDS
     fastanumer
TEMPORARYFILE survey : tester
SETTINGS
    INTERCHANGE = TRANSIT

AUXFIELDS
  dialogResult : (Fannst, EkkertFannst,HættaVið)
  searchtext   : STRING

DIALOGBOX SkolarLookup
  ESCAPE=NO
  LOOKUP Skólalisti
    SEARCH = YES
    SEARCHTEXT = '^searchtext'
    FIELDS Skolaheiti
  BUTTON dialogResult
    CAPTION = 'Velja'
    STORE = YES
    VALUE = Fannst
  BUTTON dialogResult
    CAPTION = 'Ekkert Fannst'
    STORE = YES
    VALUE = EkkertFannst
  BUTTON dialogResult
    STORE = YES
    VALUE = HættaVið
    CAPTION = 'Hætta við'

DIALOGBOX NamLookup
  ESCAPE = NO
  LOOKUP Námsgreinar
    SEARCH = YES
    SEARCHTEXT = '^searchtext'
    FIELDS Námsgrein
           StigNáms
  BUTTON dialogResult
    CAPTION = 'Velja'
    STORE = YES
    VALUE = Fannst
  BUTTON dialogResult
    CAPTION = 'Ekkert Fannst'
    STORE = YES
    VALUE = EkkertFannst
  BUTTON dialogResult
    STORE = YES
    VALUE = HættaVið
    CAPTION = 'Hætta við'


PROCEDURE prc_skoliLookup
    searchtext := survey.GETVALUE('O.OSkHvaða')
    IF ROUTERSTATUS = BLRSPREEDIT THEN
        SkolarLookup
        IF dialogResult = Fannst  THEN
          survey.putvalue('O.OSkHvaða_b',Skólalisti.Skolaheiti)
          survey.putvalue('O.Namsstig',STR(ORD(Skólalisti.stig)))
          survey.putvalue('O.OSklID',STR(skólalisti.skoliID))
          survey.PUTVALUE(ACTIVEFIELD,'Fannst')
          survey.checkrules
        ELSE
          Skólalisti.GET(9999)
          survey.putvalue('O.OSkHvaða_b',Skólalisti.Skolaheiti)
          survey.putvalue('O.OSklID',STR(skólalisti.skoliID))
          survey.PUTVALUE(ACTIVEFIELD,'EkkertFannst')
          survey.checkrules
        ENDIF
    ENDIF
    SETALIENROUTERACTION(BLRANEXTQUESTION)
ENDPROCEDURE

PROCEDURE prc_skoliLookup2
    IF ROUTERSTATUS = BLRSEDIT THEN
        searchtext := survey.GETVALUE('O.OSkHvaða')
        SkolarLookup
        IF(dialogResult<>HættaVið) THEN
           IF((Skólalisti.Skolaheiti=EMPTY) OR (dialogResult=EkkertFannst)) THEN
               Skólalisti.GET(9999)
           ELSE
               survey.putvalue('O.Namsstig',STR(ORD(Skólalisti.stig)))
           ENDIF
           survey.PUTVALUE(ACTIVEFIELD,Skólalisti.Skolaheiti)
           survey.putvalue('O.OSklID',STR(skólalisti.skoliID))
        ENDIF
        SETALIENROUTERACTION(BLRADEFAULT)
    ENDIF
ENDPROCEDURE

PROCEDURE prc_namLookup
    IF ROUTERSTATUS = BLRSEDIT THEN
        searchtext := survey.GETVALUE('O.ONamHvað')
        NamLookup
        IF(dialogResult<>HættaVið) THEN
          IF dialogResult = Fannst  THEN
            survey.putvalue(ACTIVEFIELD,Námsgreinar.Námsgrein)
            survey.putvalue('O.skóti',Námsgreinar.StigNáms)
            survey.putvalue('O.OSklKóti',STR(Námsgreinar.fastanumer))
          ELSE
            Námsgreinar.GET(9999)
            survey.putvalue(ACTIVEFIELD,Námsgreinar.Námsgrein)
            survey.putvalue('O.OSklKóti',STR(Námsgreinar.fastanumer))
          ENDIF
        ENDIF
        SETALIENROUTERACTION(BLRADEFAULT)
    ENDIF
ENDPROCEDURE



DIALOGBOX Test2
    SIZE = (360,200)
    CONTROL Skólalisti.Skolaheiti
        POSITION = (120,25)
    CONTROL Skólalisti.Stig
        POSITION = (120,50)
    BUTTON
        CAPTION = 'OK'
        SIZE = (75,25)

MANIPULATE
  searchtext := parameter(1)
  NamLookup
  Test2

